plugins {
    id 'java'
    id 'io.quarkus'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-client-jackson'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-jackson'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'com.google.genai:google-genai:1.7.0'
    implementation platform('com.google.cloud:libraries-bom:26.61.0')

    implementation 'com.google.cloud:google-cloud-storage'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.quarkus:quarkus-junit5-mockito'
}

group 'liad.dev'
version '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    useJUnitPlatform(){
        excludeTags 'integration'
    }
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests tagged with @Tag("integration")'
    group = 'verification'

    useJUnitPlatform {
        includeTags 'integration'
    }

    // Means if both test and integrationTest are scheduled to run, test will run first
    shouldRunAfter test

    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    // The following config is needed because: 
    // Gradle does not automatically assume:
    // - which test classes to run (testClassesDirs), or
    // - which classpath to use (classpath) 
    // Tells Gradle where the compiled test classes are
    testClassesDirs = sourceSets.test.output.classesDirs
    // Runtime classpath: test dependencies, Quarkus libs, etc.
    classpath = sourceSets.test.runtimeClasspath
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}
